BINARY BLACK HOLE SYSTEM UP TO 2PN - DERIVATION FOR PLOTTING OF SPINS, ANGULAR MOMENTUMS AND SYSTEM CONSERVATION CHECK

The following Mathematica notebook presents the required physics to describe the motion of a binary black hole system up to the second post-Newtonian order. Thus, gravitational wave emission is not considered and consequently the system ought to be conserved. Apart from the individual Hamiltonian order components, we provide a calculation of the system's equations of motion. The whole Mathematica code uses the formalism of the article: Integrability of eccentric, spinning black hole binaries up to second post-Newtonian order - Sashwat Tanay, Leo C. Stein, José T. Gálvez Ghersi, 2021; arXiv:2012.06586.

VECTORS

In[10]:= (*Position expressed in spherical coordinates*)xv = {r Cos[\[Phi]] Sin[\
\[Theta]], r Sin[\[Phi]] Sin[\[Theta]], r Cos[\[Theta]]}

Out[10]= {r Cos[\[Phi]] Sin[\[Theta]], r Sin[\[Theta]] Sin[\[Phi]], r Cos[\[Theta]]}

In[2]:= (*Linear momentum vector expressed in terms of momenta canonically conjugate \
to r,\[Theta],\[Phi]*)pv = {pr, p\[Phi]/(r^2 Sin[\[Theta]]^2), 
    p\[Theta]/r^2} . {D[xv, r], D[xv, \[Phi]], D[xv, \[Theta]]} // Simplify

Out[2]= {(p\[Theta] Cos[\[Theta]] Cos[\[Phi]] + pr r Cos[\[Phi]] Sin[\[Theta]] - 
  p\[Phi] Csc[\[Theta]] Sin[\[Phi]])/r, (
 p\[Phi] Cos[\[Phi]] Csc[\[Theta]] + (p\[Theta] Cos[\[Theta]] + 
     pr r Sin[\[Theta]]) Sin[\[Phi]])/r, 
 pr Cos[\[Theta]] - (p\[Theta] Sin[\[Theta]])/r}

Lv = {Lx, Ly, Lz} // FullSimplify (*Orbital angular momentum - not scaled*)

Out[1]= {Lx, Ly, Lz}

In[22]:= (*Define pv^2,pv^4,and pv^6*)pvSquared = 
 pr^2 + Lv . Lv/(G M \[Mu] r)^2 // FullSimplify

Out[22]= pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2)

In[23]:= pvFourth = pvSquared^2 // Simplify

Out[23]= (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2))^2

In[24]:= pvSixth = pvSquared^3 // Simplify

Out[24]= (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2))^3

In[6]:= (*Spin vectors*)S1v = {S1x, S1y, S1z} // FullSimplify

Out[6]= {S1x, S1y, S1z}

In[7]:= S2v = {S2x, S2y, S2z} // FullSimplify

Out[7]= {S2x, S2y, S2z}

In[8]:= (*Effective spin vector*)Seffv = (1 + 3/4 m2/m1) S1v + (1 + 3/4 m1/m2) S2v // 
  FullSimplify

Out[8]= {S1x + (3 m2 S1x)/(4 m1) + S2x + (3 m1 S2x)/(4 m2), 
 S1y + (3 m2 S1y)/(4 m1) + S2y + (3 m1 S2y)/(4 m2), 
 S1z + (3 m2 S1z)/(4 m1) + S2z + (3 m1 S2z)/(4 m2)}

In[85]:= Rhat = Lv/Norm[Lv] // 
  FullSimplify[#, 
    Assumptions -> {Lx \[Element] Reals, Ly \[Element] Reals, 
      Lz \[Element] Reals, Lx^2 + Ly^2 + Lz^2 > 0, Norm[Lv] > 0}] &

Out[85]= {Lx/Sqrt[Lx^2 + Ly^2 + Lz^2], Ly/Sqrt[Lx^2 + Ly^2 + Lz^2], Lz/Sqrt[
 Lx^2 + Ly^2 + Lz^2]}

In[86]:= (*Define the symmetric and traceless part of the second derivative*)Table[
 D2Rinv[i, j] = (3 Rhat[[i]] Rhat[[j]] - KroneckerDelta[i, j])/(r G M)^3 // 
   FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &, {i,
   1, 3}, {j, 1, 3}]

Out[86]= {{(-1 + (3 Lx^2)/(Lx^2 + Ly^2 + Lz^2))/(G^3 M^3 r^3), (3 Lx Ly)/(
  G^3 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (3 Lx Lz)/(
  G^3 (Lx^2 + Ly^2 + Lz^2) M^3 r^3)}, {(3 Lx Ly)/(
  G^3 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (-1 + (3 Ly^2)/(Lx^2 + Ly^2 + Lz^2))/(
  G^3 M^3 r^3), (3 Ly Lz)/(G^3 (Lx^2 + Ly^2 + Lz^2) M^3 r^3)}, {(3 Lx Lz)/(
  G^3 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (3 Ly Lz)/(
  G^3 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (-1 + (3 Lz^2)/(Lx^2 + Ly^2 + Lz^2))/(
  G^3 M^3 r^3)}}

NEWTONIAN  ORDER

In[26]:= H0 = \[Mu] (pvSquared/2 - 1/r) // 
  FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[26]= 1/2 (pr^2 + (-2 r + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 \[Mu]^2))/r^2) \[Mu]

In[51]:= dprdtauN = - D[H0, r] // FullSimplify

Out[51]= (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^3 \[Mu]) - \[Mu]/r^2

In[52]:= drdtauN = D[H0, pr] // FullSimplify

Out[52]= pr \[Mu]

In[30]:= OmegaLN = Grad[H0, Lv] // FullSimplify

Out[30]= {Lx/(G^2 M^2 r^2 \[Mu]), Ly/(G^2 M^2 r^2 \[Mu]), Lz/(G^2 M^2 r^2 \[Mu])}

1PN ORDER

In[31]:= H1 = (\[Mu]/c^2) ((3 \[Nu] - 1)/8 pvFourth + 1/(2 r^2) - 
     1/(2 r) ((3 + \[Nu]) pvSquared + \[Nu] pr^2)) // 
  FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[31]= (\[Mu] (4/
   r^2 + (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2))^2 (-1 + 
      3 \[Nu]) - (
   4 (((Lx^2 + Ly^2 + Lz^2) (3 + \[Nu]))/(G^2 M^2 r^2 \[Mu]^2) + 
      pr^2 (3 + 2 \[Nu])))/r))/(8 c^2)

In[49]:= dprdtau1PN = - D[H1, r] // FullSimplify

Out[49]= -((\[Mu] (-2 r + (3 (Lx^2 + Ly^2 + Lz^2) (3 + \[Nu]))/(G^2 M^2 \[Mu]^2) + 
    pr^2 r^2 (3 + 2 \[Nu]) - ((Lx^2 + Ly^2 + Lz^2) (Lx^2 + Ly^2 + Lz^2 + 
       G^2 M^2 pr^2 r^2 \[Mu]^2) (-1 + 3 \[Nu]))/(G^4 M^4 r \[Mu]^4)))/(
 2 c^2 r^4))

In[50]:= drdtau1PN = D[H1, pr] // FullSimplify

Out[50]= (\[Mu] (-((8 pr (3 + 2 \[Nu]))/r) + 
   4 pr (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2)) (-1 + 
      3 \[Nu])))/(8 c^2)

In[34]:= OmegaL1PN = Grad[H1, Lv] // FullSimplify

Out[34]= {(Lx (-2 (3 + \[Nu]) + 
    r (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2)) (-1 + 3 \[Nu])))/(
 2 c^2 G^2 M^2 r^3 \[Mu]), (
 Ly (-2 (3 + \[Nu]) + 
    r (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2)) (-1 + 3 \[Nu])))/(
 2 c^2 G^2 M^2 r^3 \[Mu]), (
 Lz (-2 (3 + \[Nu]) + 
    r (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2)) (-1 + 3 \[Nu])))/(
 2 c^2 G^2 M^2 r^3 \[Mu])}

SPIN-ORBIT (1.5PN ORDER)

In[35]:= H15 = (2)/(G^2 M^3 c^2 r^3) Dot[Lv, Seffv] // 
  FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[35]= (2 (Lx (S1x + (3 m2 S1x)/(4 m1) + S2x + (3 m1 S2x)/(4 m2)) + 
   Ly (S1y + (3 m2 S1y)/(4 m1) + S2y + (3 m1 S2y)/(4 m2)) + 
   Lz (S1z + (3 m2 S1z)/(4 m1) + S2z + (3 m1 S2z)/(4 m2))))/(c^2 G^2 M^3 r^3)

In[45]:= dprdtauSOPN = - D[H15, r] // FullSimplify

Out[45]= (6 (Lx (S1x + (3 m2 S1x)/(4 m1) + S2x + (3 m1 S2x)/(4 m2)) + 
   Ly (S1y + (3 m2 S1y)/(4 m1) + S2y + (3 m1 S2y)/(4 m2)) + 
   Lz (S1z + (3 m2 S1z)/(4 m1) + S2z + (3 m1 S2z)/(4 m2))))/(c^2 G^2 M^3 r^4)

In[37]:= drdtauSOPN = D[H15, pr]

Out[37]= 0

In[38]:= OmegaLSOPN = Grad[H15, Lv] // FullSimplify
OmegaSO1 = Grad[H15, S1v] // FullSimplify
OmegaSO2 = Grad[H15, S2v] // FullSimplify

Out[38]= {(2 (S1x + (3 m2 S1x)/(4 m1) + S2x + (3 m1 S2x)/(4 m2)))/(c^2 G^2 M^3 r^3), (
 2 (S1y + (3 m2 S1y)/(4 m1) + S2y + (3 m1 S2y)/(4 m2)))/(c^2 G^2 M^3 r^3), (
 2 (S1z + (3 m2 S1z)/(4 m1) + S2z + (3 m1 S2z)/(4 m2)))/(c^2 G^2 M^3 r^3)}

Out[39]= {(2 Lx (1 + (3 m2)/(4 m1)))/(c^2 G^2 M^3 r^3), (2 Ly (1 + (3 m2)/(4 m1)))/(
 c^2 G^2 M^3 r^3), (2 Lz (1 + (3 m2)/(4 m1)))/(c^2 G^2 M^3 r^3)}

Out[40]= {(2 Lx (1 + (3 m1)/(4 m2)))/(c^2 G^2 M^3 r^3), (2 Ly (1 + (3 m1)/(4 m2)))/(
 c^2 G^2 M^3 r^3), (2 Lz (1 + (3 m1)/(4 m2)))/(c^2 G^2 M^3 r^3)}

2PN ORDER

ORBITAL TERMS

In[41]:= H2PN = \[Mu]/
    c^4 (-1/(4 r^3) (1 + 3 \[Nu]) + 1/16 (1 - 5 \[Nu] + 5 \[Nu]^2) pvSixth + 
     1/(2 r^2) (3 \[Nu] pr^2 + (5 + 8 \[Nu]) pvSquared) + 
     1/(8 r) (-3 \[Nu]^2 pr^4 - 
        2 \[Nu]^2 pr^2 pvSquared + (5 - 20 \[Nu] - 3 \[Nu]^2) pvFourth)) // 
  FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[41]= 1/(16 c^4) \[Mu] (-((4 (1 + 3 \[Nu]))/
     r^3) + (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2))^3 (1 + 
       5 (-1 + \[Nu]) \[Nu]) + (
    8 (((Lx^2 + Ly^2 + Lz^2) (5 + 8 \[Nu]))/(G^2 M^2 r^2 \[Mu]^2) + 
       pr^2 (5 + 11 \[Nu])))/r^2 + (
    2 (-5 pr^4 \[Nu]^2 - (2 (Lx^2 + Ly^2 + Lz^2) pr^2 \[Nu]^2)/(
       G^2 M^2 r^2 \[Mu]^2) + (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(
          G^2 M^2 r^2 \[Mu]^2))^2 (5 - \[Nu] (20 + 3 \[Nu]))))/r)

In[46]:= dprdtau2PN = - D[H2PN, r] // FullSimplify

Out[46]= -1/(8 c^4 r^5) \[Mu] (5 pr^4 r^3 \[Nu]^2 + (
     2 (Lx^2 + Ly^2 + Lz^2) pr^2 r \[Nu]^2)/(G^2 M^2 \[Mu]^2) + 
     6 r (1 + 3 \[Nu]) - (16 (Lx^2 + Ly^2 + Lz^2) (5 + 8 \[Nu]))/(
     G^2 M^2 \[Mu]^2) - 8 pr^2 r^2 (5 + 11 \[Nu]) - (
     3 (Lx^2 + Ly^2 + Lz^2) (Lx^2 + Ly^2 + Lz^2 + 
        G^2 M^2 pr^2 r^2 \[Mu]^2)^2 (1 + 5 (-1 + \[Nu]) \[Nu]))/(
     G^6 M^6 r^2 \[Mu]^6) + ((Lx^2 + Ly^2 + Lz^2 + 
        G^2 M^2 pr^2 r^2 \[Mu]^2)^2 (-5 + \[Nu] (20 + 3 \[Nu])))/(
     G^4 M^4 r \[Mu]^4) + (
     4 (Lx^2 + Ly^2 + 
        Lz^2) (G^2 M^2 pr^2 r^2 \[Mu]^2 \[Nu]^2 + (Lx^2 + Ly^2 + Lz^2 + 
           G^2 M^2 pr^2 r^2 \[Mu]^2) (-5 + \[Nu] (20 + 3 \[Nu]))))/(
     G^4 M^4 r \[Mu]^4))

In[47]:= drdtau2PN = D[H2PN, pr] // FullSimplify

Out[47]= (pr \[Mu] ((40 + 88 \[Nu])/r^2 + 
   3 (pr^2 + (Lx^2 + Ly^2 + Lz^2)/(G^2 M^2 r^2 \[Mu]^2))^2 (1 + 
      5 (-1 + \[Nu]) \[Nu]) - (
   4 (Lx^2 + Ly^2 + Lz^2) (-5 + 4 \[Nu] (5 + \[Nu])))/(
   G^2 M^2 r^3 \[Mu]^2) + (4 pr^2 (5 - 4 \[Nu] (5 + 2 \[Nu])))/r))/(8 c^4)

In[48]:= OmegaL2PN =  Grad[H2PN, Lv] // FullSimplify

Out[48]= {1/(8 c^4 G^6 M^6 r^6 \[Mu]^5)
   Lx (-4 G^4 M^4 pr^2 r^3 \[Mu]^4 \[Nu]^2 + 
     8 G^4 M^4 r^2 \[Mu]^4 (5 + 8 \[Nu]) + 
     3 (Lx^2 + Ly^2 + Lz^2 + G^2 M^2 pr^2 r^2 \[Mu]^2)^2 (1 + 
        5 (-1 + \[Nu]) \[Nu]) + 
     4 G^2 M^2 r \[Mu]^2 (Lx^2 + Ly^2 + Lz^2 + 
        G^2 M^2 pr^2 r^2 \[Mu]^2) (5 - \[Nu] (20 + 3 \[Nu]))), 
 1/(8 c^4 G^6 M^6 r^6 \[Mu]^5)
   Ly (-4 G^4 M^4 pr^2 r^3 \[Mu]^4 \[Nu]^2 + 
     8 G^4 M^4 r^2 \[Mu]^4 (5 + 8 \[Nu]) + 
     3 (Lx^2 + Ly^2 + Lz^2 + G^2 M^2 pr^2 r^2 \[Mu]^2)^2 (1 + 
        5 (-1 + \[Nu]) \[Nu]) + 
     4 G^2 M^2 r \[Mu]^2 (Lx^2 + Ly^2 + Lz^2 + 
        G^2 M^2 pr^2 r^2 \[Mu]^2) (5 - \[Nu] (20 + 3 \[Nu]))), 
 1/(8 c^4 G^6 M^6 r^6 \[Mu]^5)
   Lz (-4 G^4 M^4 pr^2 r^3 \[Mu]^4 \[Nu]^2 + 
     8 G^4 M^4 r^2 \[Mu]^4 (5 + 8 \[Nu]) + 
     3 (Lx^2 + Ly^2 + Lz^2 + G^2 M^2 pr^2 r^2 \[Mu]^2)^2 (1 + 
        5 (-1 + \[Nu]) \[Nu]) + 
     4 G^2 M^2 r \[Mu]^2 (Lx^2 + Ly^2 + Lz^2 + 
        G^2 M^2 pr^2 r^2 \[Mu]^2) (5 - \[Nu] (20 + 3 \[Nu])))}

SPIN TERMS

SPIN1-SPIN1 TERM

In[87]:= HS1S1 = Simplify[(G/c^2) (m2/(2 m1)) Sum[
     S1v[[i]] S1v[[j]] D2Rinv[i, j], {i, 1, 3}, {j, 1, 3}]
   ] // FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[87]= -((m2 (-6 Ly Lz S1y S1z - 6 Lx S1x (Ly S1y + Lz S1z) + 
       Lz^2 (S1x^2 + S1y^2 - 2 S1z^2) + Ly^2 (S1x^2 - 2 S1y^2 + S1z^2) + 
       Lx^2 (-2 S1x^2 + S1y^2 + S1z^2)))/(2 c^2 G^2 (Lx^2 + Ly^2 + 
       Lz^2) M^3 m1 r^3))

In[88]:= dprdtauS1S1 = - D[HS1S1, r] // FullSimplify

Out[88]= -((3 m2 (-6 Ly Lz S1y S1z - 6 Lx S1x (Ly S1y + Lz S1z) + 
       Lz^2 (S1x^2 + S1y^2 - 2 S1z^2) + Ly^2 (S1x^2 - 2 S1y^2 + S1z^2) + 
       Lx^2 (-2 S1x^2 + S1y^2 + S1z^2)))/(2 c^2 G^2 (Lx^2 + Ly^2 + 
       Lz^2) M^3 m1 r^4))

In[89]:= drdtauS1S1 = D[HS1S1, pr] // FullSimplify

Out[89]= 0

In[90]:= OmegaLS1S1 =  Grad[HS1S1, Lv] // FullSimplify

Out[90]= {-((3 m2 (Lx S1x + Ly S1y + Lz S1z) (-Ly^2 S1x + Lx Ly S1y + 
     Lz (-Lz S1x + Lx S1z)))/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 m1 r^3)), (
 3 m2 (Lx S1x + Ly S1y + Lz S1z) (-Lx Ly S1x + Lx^2 S1y + 
    Lz (Lz S1y - Ly S1z)))/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 m1 r^3), (
 3 m2 (-Lz (Lx S1x + Ly S1y) + (Lx^2 + Ly^2) S1z) (Lx S1x + Ly S1y + 
    Lz S1z))/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 m1 r^3)}

In[91]:= OmegaS11 = Grad[HS1S1, S1v] // FullSimplify
OmegaS12 = Grad[HS1S1, S2v] // FullSimplify

Out[91]= {(m2 (2 Lx^2 S1x - (Ly^2 + Lz^2) S1x + 3 Lx (Ly S1y + Lz S1z)))/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 m1 r^3), (
 m2 (3 Lx Ly S1x - Lx^2 S1y + 2 Ly^2 S1y - Lz^2 S1y + 3 Ly Lz S1z))/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 m1 r^3), (
 3 Lz m2 (Lx S1x + Ly S1y) - (Lx^2 + Ly^2 - 2 Lz^2) m2 S1z)/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 m1 r^3)}

Out[92]= {0, 0, 0}

SPIN2-SPIN2 TERM

In[93]:= HS2S2 = Simplify[(G/c^2) (m1/(2 m2)) Sum[
      S2v[[i]] S2v[[j]] D2Rinv[i, j], {i, 1, 3}, {j, 1, 3}] /. R -> r G M
   ] // FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[93]= -((m1 (-6 Ly Lz S2y S2z - 6 Lx S2x (Ly S2y + Lz S2z) + 
       Lz^2 (S2x^2 + S2y^2 - 2 S2z^2) + Ly^2 (S2x^2 - 2 S2y^2 + S2z^2) + 
       Lx^2 (-2 S2x^2 + S2y^2 + S2z^2)))/(2 c^2 G^2 (Lx^2 + Ly^2 + 
       Lz^2) M^3 m2 r^3))

In[94]:= dprdtauS2S2 = - D[HS2S2, r] // FullSimplify

Out[94]= -((3 m1 (-6 Ly Lz S2y S2z - 6 Lx S2x (Ly S2y + Lz S2z) + 
       Lz^2 (S2x^2 + S2y^2 - 2 S2z^2) + Ly^2 (S2x^2 - 2 S2y^2 + S2z^2) + 
       Lx^2 (-2 S2x^2 + S2y^2 + S2z^2)))/(2 c^2 G^2 (Lx^2 + Ly^2 + 
       Lz^2) M^3 m2 r^4))

In[95]:= drdtauS2S2 = D[HS2S2, pr] // FullSimplify

Out[95]= 0

In[96]:= OmegaLS2S2 =  Grad[HS2S2, Lv] // FullSimplify

Out[96]= {-((3 m1 (Lx S2x + Ly S2y + Lz S2z) (-Ly^2 S2x + Lx Ly S2y + 
     Lz (-Lz S2x + Lx S2z)))/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 m2 r^3)), (
 3 m1 (Lx S2x + Ly S2y + Lz S2z) (-Lx Ly S2x + Lx^2 S2y + 
    Lz (Lz S2y - Ly S2z)))/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 m2 r^3), (
 3 m1 (-Lz (Lx S2x + Ly S2y) + (Lx^2 + Ly^2) S2z) (Lx S2x + Ly S2y + 
    Lz S2z))/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 m2 r^3)}

In[97]:= OmegaS21 = Grad[HS2S2, S1v] // FullSimplify
OmegaS22 = Grad[HS2S2, S2v] // FullSimplify

Out[97]= {0, 0, 0}

Out[98]= {(m1 (2 Lx^2 S2x - (Ly^2 + Lz^2) S2x + 3 Lx (Ly S2y + Lz S2z)))/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 m2 r^3), (
 m1 (3 Lx Ly S2x - Lx^2 S2y + 2 Ly^2 S2y - Lz^2 S2y + 3 Ly Lz S2z))/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 m2 r^3), (
 3 Lz m1 (Lx S2x + Ly S2y) - (Lx^2 + Ly^2 - 2 Lz^2) m1 S2z)/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 m2 r^3)}

SPIN1-SPIN2 TERM

In[99]:= HS1S2 = Simplify[(G/c^2) Sum[
      S1v[[i]] S2v[[j]] D2Rinv[i, j], {i, 1, 3}, {j, 1, 3}] /. R -> r G M
   ] // FullSimplify[#, {r > 0, 0 < \[Phi] < 2 \[Pi], 0 < \[Theta] < \[Pi]}] &

Out[99]= (3 Lx (Ly S1y S2x + Lz S1z S2x + Ly S1x S2y + Lz S1x S2z) + 
   3 Ly Lz (S1z S2y + S1y S2z) - Lz^2 (S1x S2x + S1y S2y - 2 S1z S2z) + 
   Lx^2 (2 S1x S2x - S1y S2y - S1z S2z) - 
   Ly^2 (S1x S2x - 2 S1y S2y + S1z S2z))/(c^2 G^2 (Lx^2 + Ly^2 + 
     Lz^2) M^3 r^3)

In[100]:= dprdtauS1S2 = - D[HS1S2, r] // FullSimplify

Out[100]= -((3 (-3 Lx (Ly S1y S2x + Lz S1z S2x + Ly S1x S2y + Lz S1x S2z) - 
       3 Ly Lz (S1z S2y + S1y S2z) + Lz^2 (S1x S2x + S1y S2y - 2 S1z S2z) + 
       Ly^2 (S1x S2x - 2 S1y S2y + S1z S2z) + 
       Lx^2 (-2 S1x S2x + S1y S2y + S1z S2z)))/(c^2 G^2 (Lx^2 + Ly^2 + 
       Lz^2) M^3 r^4))

In[101]:= drdtauS1S2 =  D[HS1S2, pr] // FullSimplify

Out[101]= 0

In[102]:= OmegaLS1S2 =  Grad[HS1S2, Lv] // FullSimplify

Out[102]= {(-3 Lx^2 (Ly S1y S2x + Lz S1z S2x + Ly S1x S2y + Lz S1x S2z) + 
    3 (Ly^2 + Lz^2) (Ly S1y S2x + Lz S1z S2x + Ly S1x S2y + Lz S1x S2z) + 
    6 Lx (Ly^2 (S1x S2x - S1y S2y) - Ly Lz (S1z S2y + S1y S2z) + 
       Lz^2 (S1x S2x - S1z S2z)))/(c^2 G^2 (Lx^2 + Ly^2 + 
      Lz^2)^2 M^3 r^3), (3 (Lx^3 (S1y S2x + S1x S2y) - 
      Lx Ly^2 (S1y S2x + S1x S2y) + Lx Lz^2 (S1y S2x + S1x S2y) - 
      2 Lx Ly Lz (S1z S2x + S1x S2z) - Ly^2 Lz (S1z S2y + S1y S2z) + 
      Lz^3 (S1z S2y + S1y S2z) + 
      Lx^2 (-2 Ly S1x S2x + 2 Ly S1y S2y + Lz S1z S2y + Lz S1y S2z) + 
      2 Ly Lz^2 (S1y S2y - S1z S2z)))/(c^2 G^2 (Lx^2 + Ly^2 + 
      Lz^2)^2 M^3 r^3), (3 (-2 Lz (Lx S1x + Ly S1y) + (Lx^2 + Ly^2 - 
          Lz^2) S1z) (Lx S2x + Ly S2y) + 
    3 ((Lx^2 + Ly^2 - Lz^2) (Lx S1x + Ly S1y) + 
       2 (Lx^2 + Ly^2) Lz S1z) S2z)/(c^2 G^2 (Lx^2 + Ly^2 + Lz^2)^2 M^3 r^3)}

In[103]:= OmegaS121 = Grad[HS1S2, S1v] // FullSimplify
OmegaS122 = Grad[HS1S2, S2v] // FullSimplify

Out[103]= {(2 Lx^2 S2x - (Ly^2 + Lz^2) S2x + 3 Lx (Ly S2y + Lz S2z))/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (
 3 Lx Ly S2x - Lx^2 S2y + 2 Ly^2 S2y - Lz^2 S2y + 3 Ly Lz S2z)/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (
 3 Lz (Lx S2x + Ly S2y) - (Lx^2 + Ly^2 - 2 Lz^2) S2z)/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 r^3)}

Out[104]= {(2 Lx^2 S1x - (Ly^2 + Lz^2) S1x + 3 Lx (Ly S1y + Lz S1z))/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (
 3 Lx Ly S1x - Lx^2 S1y + 2 Ly^2 S1y - Lz^2 S1y + 3 Ly Lz S1z)/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 r^3), (
 3 Lz (Lx S1x + Ly S1y) - (Lx^2 + Ly^2 - 2 Lz^2) S1z)/(
 c^2 G^2 (Lx^2 + Ly^2 + Lz^2) M^3 r^3)}
